# ----------------------------------------------------------------------- #
# -------------------------- Set CMake version -------------------------- #
# ----------------------------------------------------------------------- #
cmake_minimum_required(VERSION 3.11)
# ----------------------------------------------------------------------- #
# -------------------------- Fetch dependencies ------------------------- #
# ----------------------------------------------------------------------- #
project(grpc NONE)

set(deps @SPECTER_BUILD_DEPS_DIR@)
set(prefix_dir ${deps}/grpc)
set(src_dir ${prefix_dir}/src/grpc)
set(build_dir ${prefix_dir}/src/grpc-build)
set(install_dir ${prefix_dir}/src/grpc-install)

if(CMAKE_CONFIGURATION_TYPES)
    set(BUILD_COMMAND_CONFIG 
        ${CMAKE_COMMAND} 
        --build ${build_dir}
        --config $<CONFIG>)
    
    set(INSTALL_COMMAND_CONFIG
        ${CMAKE_COMMAND} 
        --build ${build_dir} 
        --target install
        --config $<CONFIG>)
else()
    if(NOT CMAKE_BUILD_TYPE)
        set(CMAKE_BUILD_TYPE "Release" CACHE STRING "Choose the type of build." FORCE)
    endif()
    
    set(BUILD_COMMAND_CONFIG 
        ${CMAKE_COMMAND} 
        --build ${build_dir}
        --config ${CMAKE_BUILD_TYPE})
    
    set(INSTALL_COMMAND_CONFIG
        ${CMAKE_COMMAND} 
        --build ${build_dir} 
        --target install
        --config ${CMAKE_BUILD_TYPE})
endif()

include(ExternalProject)
ExternalProject_Add(
    grpc
    GIT_REPOSITORY      
        https://github.com/grpc/grpc
    GIT_TAG             
        v1.66.1
    PREFIX              
        ${prefix_dir}
    CONFIGURE_COMMAND   
        ${CMAKE_COMMAND} 
        -B ${build_dir} 
        -G "${CMAKE_GENERATOR}" 
        -DCMAKE_INSTALL_PREFIX=${install_dir} 
        -DgRPC_INSTALL=ON
        -DgRPC_BUILD_PROTOBUF=ON
        -DgRPC_INSTALL_PROTOBUF=ON
        ${CMAKE_CACHE_ARGS}
        ${src_dir}
    CMAKE_CACHE_ARGS
        -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
        -DCMAKE_C_FLAGS:STRING=${CMAKE_C_FLAGS}
        -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
        -DCMAKE_MSVC_RUNTIME_LIBRARY:STRING=${CMAKE_MSVC_RUNTIME_LIBRARY}
        -DCMAKE_CXX_FLAGS_DEBUG:STRING=${CMAKE_CXX_FLAGS_DEBUG}
        -DCMAKE_CXX_FLAGS_RELEASE:STRING=${CMAKE_CXX_FLAGS_RELEASE}
        -DCMAKE_C_FLAGS_DEBUG:STRING=${CMAKE_C_FLAGS_DEBUG}
        -DCMAKE_C_FLAGS_RELEASE:STRING=${CMAKE_C_FLAGS_RELEASE}
        -DCMAKE_EXE_LINKER_FLAGS:STRING=${CMAKE_EXE_LINKER_FLAGS}
        -DCMAKE_SHARED_LINKER_FLAGS:STRING=${CMAKE_SHARED_LINKER_FLAGS}
        -DCMAKE_MODULE_LINKER_FLAGS:STRING=${CMAKE_MODULE_LINKER_FLAGS}
    BUILD_COMMAND       
        ${BUILD_COMMAND_CONFIG}
    INSTALL_COMMAND     
        ${INSTALL_COMMAND_CONFIG}
    TEST_COMMAND        
        "")
# ----------------------------------------------------------------------- #