# ----------------------------------------------------------------------- #
# -------------------------- Set CMake version -------------------------- #
# ----------------------------------------------------------------------- #
cmake_minimum_required(VERSION 3.11)
# ----------------------------------------------------------------------- #
# -------------------------- Fetch dependencies ------------------------- #
# ----------------------------------------------------------------------- #
project(qt NONE)

set(prefix_dir @SPECTER_BUILD_DEPS_DIR@/qt)
set(install_dir ${prefix_dir}/src/qt-install)

if(WIN32)
    set(configure_script ./configure.bat)
else()
    set(configure_script ./configure)
endif()

if(CMAKE_CONFIGURATION_TYPES)
    set(BUILD_COMMAND_CONFIG 
        ${CMAKE_COMMAND} 
        --build .
        --config $<CONFIG>)
    
    set(INSTALL_COMMAND_CONFIG
        ${CMAKE_COMMAND} 
        --build .
        --target install)
else()
    if(NOT CMAKE_BUILD_TYPE)
        set(CMAKE_BUILD_TYPE "Release" CACHE STRING "Choose the type of build." FORCE)
    endif()
    
    set(BUILD_COMMAND_CONFIG 
        ${CMAKE_COMMAND} 
        --build .
        --config ${CMAKE_BUILD_TYPE})
    
    set(INSTALL_COMMAND_CONFIG
        ${CMAKE_COMMAND} 
        --build .
        --target install)
endif()

include(ExternalProject)
ExternalProject_Add(
    qt
    URL                         
        https://download.qt.io/official_releases/qt/6.9/6.9.0/single/qt-everywhere-src-6.9.0.tar.xz
    URL_MD5                     
        6c3e4eceffe8a0e76c7a89a74d33be00
    PREFIX                      
        ${prefix_dir}
    BUILD_IN_SOURCE             
        TRUE
    DOWNLOAD_EXTRACT_TIMESTAMP  
        TRUE
    CONFIGURE_COMMAND           
        ${configure_script} 
        -confirm-license -opensource 
        -skip qtwebengine -skip qtwebview
        -nomake examples -nomake tests 
        -qpa xcb -prefix ${install_dir}
    BUILD_COMMAND               
        ${BUILD_COMMAND_CONFIG} 
    INSTALL_COMMAND             
        ${INSTALL_COMMAND_CONFIG} 
    TEST_COMMAND                
        "")
# ----------------------------------------------------------------------- #